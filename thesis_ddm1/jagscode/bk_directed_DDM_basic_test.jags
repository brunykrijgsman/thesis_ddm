
model {
    ##########
    # Parameter priors
    ##########
    for (p in 1:nparts) {
        # Boundary parameter (speed-accuracy tradeoff) per participant
        alpha[p] ~ dnorm(1, pow(.5,-2))T(0, 3)

        # Non-decision time per participant
        ndt[p] ~ dnorm(.5, pow(.25,-2))T(0, 1)

        # Start point bias towards choice A per participant
        beta[p] ~ dnorm(.5, pow(.25,-2))T(0, 1)

        # Mean of latent variable per participant
        mu_z[p] ~ dnorm(0, pow(2, -2))
        
        # SD of latent variable per participant
        sigma_z[p] ~ dnorm(1, pow(.5,-2))T(0, 3)
        
        # Scaling parameter for the drift rate per trial per participant
        lambda[p] ~ dnorm(1, pow(.5,-2))
        
        # Baseline drift per participant
        b[p] ~ dnorm(0, pow(2, -2))

        # Trial-to-trial variability per participant
        eta[p] ~ dnorm(0.5, pow(.25,-2))T(0, 2)
    }

    ##########
    # Model likelihood
    ##########
    for (i in 1:N) {
        # Latent variable (P300)
        z[i] ~ dnorm(mu_z[participant[i]], pow(sigma_z[participant[i]], -2))
        
        # Drift rate
        delta[i] <- lambda[participant[i]] * z[i] + b[participant[i]] + eta[participant[i]]
        
        # DDM process (acc*RT)
        y[i] ~ dwiener(alpha[participant[i]], ndt[participant[i]], 
                       beta[participant[i]], delta[i])
    }
}
